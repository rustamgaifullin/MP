apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: 'kotlin-android-extensions'

buildscript {
    ext {
        kotlin_version = "1.3.21"
        rxjava_version = "2.1.17"
        rxkotlin_version = "2.2.0"
        mockito_version = "2.18.0"
        junit_version = "4.12"
        junit_platform_runner_version = "1.0.0-M4"
        support_library_version = "28.0.0"
        constraintlayout_version = "1.1.3"
        rxandroid_version = "2.0.2"
        android_espresso_version = "3.0.1"
        android_support_test_version = "1.0.1"
        uiautomator_version = "2.1.2"
        mockito_kotlin_version = "1.5.0"
        room_version = "1.1.1"
        google_api_version = "1.23.0"
        google_sheets_version = "v4-rev486-$google_api_version"
        google_drive_version = "v3-rev85-$google_api_version"
        google_play_services_version = "16.0.1"
        easy_permission_version = "0.3.0"
        dagger_version = "2.15"
        multidex_version = "1.0.3"
        dex_opener_version = "0.12.1"
    }

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junit_platform_runner_version"
        classpath "org.junit.platform:junit-platform-runner:$junit_platform_runner_version"
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "io.rg.mp"
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.github.tmurakami.dexopener.DexOpenerAndroidJUnitRunner"

        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            minifyEnabled false //not working with true...probably for release type also
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    //Conflict with dependency "com.google.code.findbugs:jsr305". Resolved versions for app (3.0.1) and test app (2.0.1) differ
    //That"s why
    configurations.all {
        resolutionStrategy.force "com.google.code.findbugs:jsr305:3.0.1"
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "com.android.support.constraint:constraint-layout:$constraintlayout_version"
    implementation "com.android.support:appcompat-v7:$support_library_version"
    implementation "com.android.support:design:$support_library_version"
    implementation "com.android.support:support-annotations:$support_library_version"
    implementation "com.android.support:support-v4:$support_library_version"
    implementation "com.android.support:cardview-v7:$support_library_version"

    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlin_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"

    implementation("com.google.api-client:google-api-client-android:$google_api_version") {
        exclude group: "org.apache.httpcomponents"
    }
    implementation("com.google.apis:google-api-services-sheets:$google_sheets_version") {
        exclude group: "org.apache.httpcomponents"
    }
    implementation("com.google.apis:google-api-services-drive:$google_drive_version") {
        exclude group: "org.apache.httpcomponents"
    }

    implementation "com.google.android.gms:play-services-auth:$google_play_services_version"
    implementation "pub.devrel:easypermissions:$easy_permission_version"

    implementation "android.arch.persistence.room:runtime:$room_version"
    implementation "android.arch.persistence.room:rxjava2:$room_version"

    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"

    implementation "com.android.support:multidex:$multidex_version"

    kapt "android.arch.persistence.room:compiler:$room_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    androidTestImplementation "com.android.support:support-annotations:$support_library_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$android_espresso_version"
    androidTestImplementation "com.android.support.test:runner:$android_support_test_version"
    androidTestImplementation "com.android.support.test:rules:$android_support_test_version"
    androidTestImplementation "com.android.support.test.uiautomator:uiautomator-v18:$uiautomator_version"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
    androidTestImplementation "com.nhaarman:mockito-kotlin:$mockito_kotlin_version"
    androidTestImplementation "com.github.tmurakami:dexopener:$dex_opener_version"
    androidTestImplementation ("android.arch.core:core-testing:$room_version") {
        exclude module: 'support-annotations'
    }

    testImplementation "junit:junit:$junit_version"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation "org.junit.platform:junit-platform-runner:$junit_platform_runner_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "org.mockito:mockito-inline:$mockito_version"
    testImplementation "com.nhaarman:mockito-kotlin:$mockito_kotlin_version"
}